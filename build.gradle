description = 'Java MYA API'
version = '2.2.0'
group 'org.jlab'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

dependencies {
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.42'
    //runtime files('config')
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.7.0'
    testCompile group: 'org.apache.commons', name: 'commons-pool2', version: '2.7.0'
}

compileJava   {
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

task hello (type: JavaExec) {
    group 'Application'
    description 'Hello World Test'
    main = 'org.jlab.mya.HelloWorld'
    classpath = sourceSets.test.runtimeClasspath
}

task config {
    doLast {
        def console = System.console()
        if (console) {
            def username = console.readLine('> Please enter mya username: ')
            def password = console.readPassword('> Please enter mya password: ')
            new File("$projectDir/src/main/resoureces/credentials.properties").text = """username=$username
password=$password
"""
        } else {
            logger.error "Cannot get console, try running as: 'gradlew -Dorg.gradle.daemon=false config'"
        }
    }
}

// This is dumb, but works.  I can't get Gradle to add config dir to classpath when running JUnit tests so I move the
// properties files where Gradle expects them.   But now I have to tell Gradle to not package our passwords
// into the jar file!
jar {
    exclude('credentials.properties', 'deployments.properties', 'credentials.properties.template', 'deployments.properties.template')
}

javadoc {
    options.overview = "src/overview.html"
    //source = sourceSets.main.allJava
    //classpath = sourceSets.main.allSource
    options.with {
        links 'https://docs.oracle.com/javase/8/docs/api/'
        //links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://build.acc.jlab.org/artifactory/plugins-release'
        }
         
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.10.0"
    }
}

allprojects {
    apply plugin: "com.jfrog.artifactory"
}
 
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "http://build.acc.jlab.org/artifactory"
    publish {
        repository {
            repoKey = 'gradle-release-local'
            maven = true
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-dev'
            maven = true
             
        }
    }
}
