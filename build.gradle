description = 'Java MYA API'
version = '2.2.0'
group 'org.jlab'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    jcenter()
}

dependencies {
    compile group: 'mysql', name: 'mysql-connector-java', version: '5.1.42'
    runtime files('config')
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1.1'
    testCompile group: 'org.apache.commons', name: 'commons-pool2', version: '2.4.2'
}

sourceSets.main.java.srcDirs = ['src']
sourceSets.test.java.srcDirs = ['test']

compileJava {
    options.compilerArgs += ["-Xlint:deprecation"]
}

task hello (type: JavaExec) {
    group 'Application'
    description 'Hello World Test'
    main = 'org.jlab.mya.HelloWorld'
    classpath = sourceSets.test.runtimeClasspath
}

task config {
    doLast {
        def console = System.console()
        if (console) {
            def username = console.readLine('> Please enter mya username: ')
            def password = console.readPassword('> Please enter mya password: ')
            new File("$projectDir/config/credentials.properties").text = """username=$username
password=$password
"""
        } else {
            logger.error "Cannot get console, try running as: 'gradlew -Dorg.gradle.daemon=false config'"
        }
    }
}

task doc (type: Javadoc) {
  source = sourceSets.main.allJava
  options.overview = "src/overview.html"
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://build.acc.jlab.org/artifactory/plugins-release'
        }
         
    }
    dependencies {
        //Check for the latest version here: http://plugins.gradle.org/plugin/com.jfrog.artifactory
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.10.0"
    }
}

allprojects {
    apply plugin: "com.jfrog.artifactory"
}
 
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    contextUrl = "http://build.acc.jlab.org/artifactory"
    publish {
        repository {
            repoKey = 'gradle-release-local'
            maven = true
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        defaults {
            publications('mavenJava')
            publishArtifacts = true
            publishPom = true
        }
    }
    resolve {
        repository {
            repoKey = 'gradle-dev'
            maven = true
             
        }
    }
}
